plugins {
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'eclipse'
    id 'dk.mada.style' version '0.9.6'
    id 'dk.mada.buildinfo' version '0.9.0'
}

ext {
    mavenDisplayName = 'Plugin for creating buildinfo for maven artifacts'
    mavenDescription = 'A plugin that creates buildinfo for maven artifacts.'
}

group = 'dk.mada.buildinfo'

repositories {
    repositories {
        maven { url = uri("https://plugins.gradle.org/m2/") }
    }
    mavenCentral()
}

configurations {
    addedDependencies
}

dependencies {
    compileOnly        'org.jspecify:jspecify:0.3.0'

    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly    'org.junit.platform:junit-platform-launcher'

    testImplementation gradleTestKit()
    testImplementation 'org.assertj:assertj-core:3.25.3'
}

gradlePlugin {
    website = 'https://github.com/jskov/buildinfo-gradle'
    vcsUrl = 'https://github.com/jskov/buildinfo-gradle'
    plugins {
        stylePlugin {
            id = 'dk.mada.buildinfo'
            displayName = mavenDisplayName
            description = mavenDescription
            tags.set(['buildinfo', 'reproducible'])
            implementationClass = 'dk.mada.buildinfo.BuildinfoPlugin'
        }
    }
    
    testSourceSets sourceSets.test
}

publishing {
    repositories {
        maven {
            name = "dist"
            url "file://${project.rootProject.file("build/dist").getAbsolutePath()}"
        }
    }
}

project.afterEvaluate { p ->
    tasks.withType(GenerateMavenPom).all {
        getPom().url = 'https://github.com/jskov/buildinfo-gradle'
        getPom().name = mavenDisplayName
        getPom().description = mavenDescription

        getPom().developers {
            developer {
                id = 'jskov'
                name = 'Jesper Skov'
                email = 'jskov@mada.dk'
            }
        }
        getPom().licenses {
            license {
                name = 'Licensed under the EUPL-1.2-or-later'
                url = 'https://joinup.ec.europa.eu/sites/default/files/custom-page/attachment/2020-03/EUPL-1.2%20EN.txt'
            }
        }
        getPom().scm {
            connection = 'scm:git:git://github.com/jskov/buildinfo-gradle.git'
            developerConnection = 'scm:git:ssh://github.com:jskov/buildinfo-gradle.git'
            url = 'https://github.com/jskov/buildinfo-gradle/'
        }
    }
}

// Makes it possible to run Plugin tests
tasks.eclipse {
    dependsOn('processResources')
    doLast {
        project.delete("build/pluginUnderTestMetadata")
        project.mkdir("build/pluginUnderTestMetadata")
        project.file("./build/pluginUnderTestMetadata/plugin-under-test-metadata.properties") <<
            "implementation-classpath=${project.rootDir}/bin/main:${project.rootDir}/build/resources/main\n" 
   }
}

test {
    useJUnitPlatform()
    
    inputs.file("src/test/data/plugin/expected-buildinfo.txt")
    
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}
